plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = 'https://maven.fabricmc.net/' }
	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url = 'https://jitpack.io' }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"modid" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

configurations {
    shadowOnly
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "me.lucko:fabric-permissions-api:${project['fabric-permissions-api']}"
	implementation "com.electronwill.night-config:core:${project.night_config_version_core}"
	implementation "com.electronwill.night-config:toml:${project.night_config_version_toml}"
	shadowOnly "com.electronwill.night-config:core:${project.night_config_version_core}"
	shadowOnly "com.electronwill.night-config:toml:${project.night_config_version_toml}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}" }
	}
}

shadowJar {
    configurations = [project.configurations.shadowOnly]
    relocate 'com.electronwill.nightconfig', 'org.samo_lego.antilogout.shadow.nightconfig'
}
remapJar {
    dependsOn shadowJar
    input.set(shadowJar.archiveFile)
}

def ENV = System.getenv()

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
	repositories {
		// Add repositories to publish to here.
	}
}